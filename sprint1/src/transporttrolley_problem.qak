System trolley_problem

// mock wasteservice
Dispatch simulate : simulate(LOCATION)

// transporttrolley
Request move : move(LOCATION)
Reply moveDone : moveDone(ANSWER)

// trolleymover
Request doPath : doPath(PATH, OWNER)
Reply doPathDone : doPathDone(ARG)
Reply doPathFail : doPathFail(ARG)
Request stopPath : stopPath(_)
Reply stopACK : stopACK(_)

// basicrobot
Dispatch cmd : cmd(MOVE)
Request step : step(TIME)
Reply stepdone : stepdone(V)
Reply stepfail : stapfail(V)
Event alarm : alarm(X)

//-------------------------------------------------------------------------------------------------//
//---------------------------------------CONTEXTS--------------------------------------------------//
//-------------------------------------------------------------------------------------------------//
Context ctxbasicrobot ip[host="localhost" port=8020]
Context ctxwasteservice ip[host="127.0.0.1" port=8038]

//-------------------------------------------------------------------------------------------------//
//---------------------------------------ACTORS----------------------------------------------------//
//-------------------------------------------------------------------------------------------------//
ExternalQActor basicrobot context ctxbasicrobot

//-------------------------------------------------------------------------------------------------//
//---------------------------------------CTXWASTESERVICE-------------------------------------------//
//-------------------------------------------------------------------------------------------------//

/*
QActor mockwasteservice context ctxwasteservice {
	
	State init initial {
		forward transporttrolley -m simulate : simulate(PLASTICBOX)
		delay 6432
		forward transporttrolley -m simulate : simulate(INDOOR)
	}
}

QActor transporttrolley context ctxwasteservice {
	[# var LOC: String = "" #]	
	
	State init initial {
		println("\tTRANSPORTTROLLEY | started.")
	}
	Goto idle
	
	State idle {
		println("\tTRANSPORTTROLLEY | waiting...")
	}
	Transition t0 whenMsg simulate -> handleMovment
	
	State handleMovment {		
		onMsg(simulate : simulate(LOCATION)) {
			[#
				LOC = payloadArg(0)
			#]
		}
		
		request trolleymover -m move : move($LOC)
	}
	Transition t1 whenMsg simulate -> handleMovment
				  whenReply moveDone -> handleMoveDone
				  

	State handleMoveDone {
		println("\tTRANSPORTTROLLEY | moveDone.")
	}	
	Goto idle
}
*/

QActor trolleymover context ctxwasteservice {
	[# 
		var Actions: String = "" 
		var LOC: String = ""
		var IsMoving = false
		var Progress = ""
		SystemConfig.setTheConfiguration("SystemConfiguration")
		planner.initAI()
		planner.loadRoomMap("mapRoomEmpty")
	#]
	
	State init initial {
		discardMsg On
		println("\tTROLLEYMOVER | started.")
	}
	Goto idle
	
	State idle {
		println("\tTROLLEYMOVER | waiting...")
		[# IsMoving = false #]
	}
	Transition t0 whenRequest move -> handleMovement
	
	State handleMovement {
		onMsg(move : move(LOCATION)) {
			[# LOC = payloadArg(0) #]
		}
		
		println("\tTROLLEYMOVER | received movement to $LOC")
				
		
		if [# IsMoving #] {
			println("\tTROLLEYMOVER | arrived move command when moving.")
			
			updateResource [# "trolleymover(handleMovement_stopPath)" #]
			
			request pathexecutor -m stopPath : stopPath(_)
		} else {
			[#
				var coord = utils.getClosestCoordinate(planner.get_curCoord(), LOC)
				planner.setGoal(coord.x, coord.y)
				planner.doPlan()
				Actions = planner.getActionsString()
				IsMoving = true
			#]

			println("\tTROLLEYMOVER | actions: $Actions")
			updateResource [# "trolleymover(handleMovement_$LOC)" #]
			request pathexecutor -m doPath : doPath($Actions, trolleymover)
		}
	}
	Transition t1 whenReply doPathDone -> handlePathDone
				  whenReply doPathFail -> handlePathFail
				  whenReply stopACK -> handleInterruptedMovement
				  whenRequest move -> handleMovement
				  
	State handleInterruptedMovement {
		onMsg(stopACK : stopACK(_)) {
			[# IsMoving = false #]
			updateResource [# "trolleymover(handleInterruptedMovement)" #]
		}
	}	
	Goto handleMovement			  
				  
	State handlePathDone {
		[# IsMoving = false #]
		println("\tTROLLEYMOVER | arrived to $LOC")
		updateResource [# "trolleymover(handlePathDone_$LOC)" #]
		replyTo move with moveDone : moveDone(OK)
	}	
	Goto idle
	
	State handlePathFail {
		[# IsMoving = false #]
		println("\tTROLLEYMOVER | path failed to $LOC")
		updateResource [# "trolleymover(handlePathFail_$LOC)" #]
		replyTo move with moveDone : moveDone(NO)
	}
	Goto idle
}

QActor pathexecutor context ctxwasteservice {
	[# 
		var CurMoveTodo = ""
		var MovesDone = "" 
	#]
	
	State init initial {
		[# CurMoveTodo = "" #]
		discardMsg On
		[# sysUtil.logMsgs=true #]
		println("\tPATHEXECUTOR | started")
	}
	Transition t0 whenRequest doPath -> doThePath
	
	State doThePath {
		onMsg(doPath : doPath(P, C)) {
			[# val path = payloadArg(0); println(path) #]
			[# pathut.setPath(path) #]
		}
		
		println("\tPATHEXECUTOR | pathTodo: ${pathut.getPathTodo()}")
	}
	Goto nextMove
	
	State nextMove {
		[# CurMoveTodo = pathut.nextMove() #]
		[# MovesDone += CurMoveTodo #]
		println("\tPATHEXECUTOR | curMoveTodo: $CurMoveTodo")
	}
	Goto endWorkOk if [# CurMoveTodo.length == 0 #] else doMove
	
	State handleStopPath {
		replyTo stopPath with stopACK : stopACK(_) caller==trolleymover
		[# MovesDone = "" #]
	}
	Goto init
	
	State doMove {
		[#
			planner.updateMap(CurMoveTodo, "")
			// planner.showMap()
			// planner.showCurrentRobotState()	
		#]
		delay 350
	}
	Goto doMoveW if [# CurMoveTodo == "w" #] else doMoveTurn
	
	State doMoveTurn {
		forward basicrobot -m cmd : cmd($CurMoveTodo)
	}
	Transition t1 whenTime 350 -> nextMove
			
	State doMoveW {
		request basicrobot -m step : step(350)
	}
	Transition t2 whenEvent alarm -> handleAlarm
				  whenReply stepdone -> nextMove
				  whenReply stepfail -> endWorkKo
				  whenRequest stopPath -> handleStopPath
				  
	State handleAlarm {
		[# var PathTodo = pathut.getPathTodo() #]
		println("\tPATHEXECUTOR | handleAlarm ... pathTodo: $PathTodo")
	}
	
	State endWorkOk {
		println("\tPATHEXECUTOR | Path done - bye")
		replyTo doPath with doPathDone : doPathDone(ok) caller==trolleymover
	}
	Goto init
	
	State endWorkKo {
		[# var PathStillTodo = pathut.getPathTodo() #]
		println("\tPATHEXECUTOR | path failure - sorry. PathStillTodo: $PathStillTodo")
		replyTo doPath with doPathFail : doPathFail($PathStillTodo) caller==trolleymover
	}
	Transition t0 whenEvent alarm -> handleAlarm
}